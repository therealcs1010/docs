// Gradle Configuration File
// For more details take a look at the Java Quickstart chapter in the Gradle
// user guide available at http://gradle.org/docs/4.8.1/userguide/tutorial_java_projects.html

import org.gradle.api.tasks.testing.logging.TestLogEvent


plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.asciidoctor.convert' version '1.5.6'
}

checkstyle {
    toolVersion = '8.23'
}

shadowJar {
    archiveBaseName = "AlgoSenpai"
    archiveVersion = "0.1.0"
    archiveClassifier = null
    archiveAppendix = null
}


group 'com.algosenpai.app'
version '0.1.0'

repositories {
    mavenCentral()
}

application {
    mainClassName = "com.algosenpai.app.Launcher"
}

run {
    standardInput = System.in
}

dependencies {
    implementation 'com.itextpdf:itextpdf:5.5.10'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.56'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0'
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task headless {
    doLast {
        println 'Setting headless mode properties.'
        test {
        }
    }
}

// Makes sure that headless properties are set before running tests
test.mustRunAfter headless

asciidoctor {
    backends 'html5'
    sourceDir 'docs'
    outputDir "${buildDir}/docs"

    options = [
            template_dirs: [file("${sourceDir}/templates")],
    ]

    attributes = [
            linkcss             : true,
            stylesheet          : 'gh-pages.css',
            'source-highlighter': 'coderay',
            icons               : 'font',
            experimental        : true,
            sectlinks           : true,
            idprefix            : '',  // for compatibility with GitHub preview
            idseparator         : '-',
            'site-root'         : "${sourceDir}",  // must be the same as sourceDir, do not modify
            'site-name'         : 'AddressBook-Level4',
            'site-githuburl'    : 'https://github.com/se-edu/addressbook-level4',
            'site-seedu'        : true,  // delete this line if your project is not a fork (not a SE-EDU project)
    ]

    options['template_dirs'].each {
        inputs.files fileTree(it)
    }
}

// Copies stylesheets into the directory containing generated HTML files as
// Asciidoctor does not copy linked CSS files to the output directory when rendering.
// This is needed for linked stylesheets and embedded stylesheets which import other files.
task copyStylesheets(type: Copy) {
    from "${asciidoctor.sourceDir}/stylesheets"
    into "${asciidoctor.outputDir}/html5/stylesheets"
}
asciidoctor.dependsOn copyStylesheets

task deployOfflineDocs(type: Copy) {
    into('src/main/resources/docs')

    from("${asciidoctor.outputDir}/html5") {
        include 'stylesheets/*'
        include 'images/*'
        include 'HelpWindow.html'
    }
}

deployOfflineDocs.dependsOn asciidoctor
processResources.dependsOn deployOfflineDocs

defaultTasks 'clean', 'headless', 'allTests', 'coverage', 'asciidoctor'
